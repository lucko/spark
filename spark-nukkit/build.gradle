plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.1'
}

dependencies {
    compile project(':spark-common')
    compile('net.kyori:adventure-text-serializer-legacy:4.4.0')
    compileOnly 'cn.nukkit:nukkit:1.0-SNAPSHOT'
}

repositories {
    maven { url "https://repo.opencollab.dev/maven-snapshots/" }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'plugin.yml'
    }
}

shadowJar {
    archiveName = 'spark-nukkit.jar'

    relocate 'okio', 'me.lucko.spark.lib.okio'
    relocate 'okhttp3', 'me.lucko.spark.lib.okhttp3'
    relocate 'org.tukaani.xz', 'me.lucko.spark.lib.xz'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
