plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'io.papermc.paperweight.userdev' version '1.7.2'
}

tasks.withType(JavaCompile) {
    options.release = 21
}

dependencies {
    implementation project(':spark-common')
    implementation 'net.kyori:adventure-platform-bukkit:4.3.3'
    paperweight.foliaDevBundle("1.20.6-R0.1-SNAPSHOT")
    
    // placeholders
    compileOnly 'me.clip:placeholderapi:2.10.3'
    compileOnly('be.maximvdw:MVdWPlaceholderAPI:3.0.1-SNAPSHOT') {
        exclude(module: 'MVdWUpdater')
    }
}

repositories {
    maven { url "https://repo.papermc.io/repository/maven-public/" }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'plugin.yml'
    }
}

tasks {
    assemble {
        dependsOn reobfJar
    }
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-bukkit.jar"

    relocate 'net.kyori.adventure', 'me.lucko.spark.lib.adventure'
    relocate 'net.kyori.examination', 'me.lucko.spark.lib.adventure.examination'
    relocate 'net.kyori.option', 'me.lucko.spark.lib.adventure.option'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
    exclude '**/*.proto'
    exclude '**/*.proto.bin'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
