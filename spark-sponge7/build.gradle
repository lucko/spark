plugins {
    id 'net.kyori.blossom' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    implementation project(':spark-common')
    compileOnly 'org.spongepowered:spongeapi:7.3.0'
    annotationProcessor 'org.spongepowered:spongeapi:7.3.0'
}

repositories {
    maven { url "https://repo.spongepowered.org/maven" }
}

blossom {
    replaceTokenIn('src/main/java/me/lucko/spark/sponge/Sponge7SparkPlugin.java')
    replaceToken '@version@', project.pluginVersion
    replaceToken '@desc@', project.pluginDescription
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-sponge7.jar"

    relocate 'net.kyori.adventure', 'me.lucko.spark.lib.adventure'
    relocate 'net.kyori.examination', 'me.lucko.spark.lib.adventure.examination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
    exclude 'META-INF/versions/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
