plugins {
    id 'io.github.goooler.shadow' version '8.1.7'
}

dependencies {
    implementation project(':spark-common')
    implementation project(':spark-bukkit-common')
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'

    // placeholders
    compileOnly 'me.clip:placeholderapi:2.10.3'
    compileOnly('be.maximvdw:MVdWPlaceholderAPI:3.0.1-SNAPSHOT') {
        exclude(module: 'MVdWUpdater')
    }
}

repositories {
    maven { url "https://repo.papermc.io/repository/maven-public/" }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'plugin.yml'
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 17
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-paper.jar"

    dependencies {
        exclude(dependency('net.kyori:^(?!adventure-text-feature-pagination).+$'))
    }

    relocate 'net.kyori.adventure.text.feature.pagination', 'me.lucko.spark.lib.adventure.pagination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
