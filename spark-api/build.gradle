plugins {
    id 'maven-publish'
}

version = api_version
group = 'me.lucko.spark'
archivesBaseName = 'api'

dependencies {
    api project(':spark-proto')
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.checkerframework:checker-qual:3.22.1'
    compileOnly 'com.google.errorprone:error_prone_annotations:2.6.0'
}

license {
    header = project.file('HEADER.txt')
}

java.withSourcesJar()
java.withJavadocJar()

jar {
    manifest.attributes([
            "Specification-Title"     : 'SparkAPI',
            "Specification-Vendor"    : 'Lucko',
            "Specification-Version"   : '1', // We are version 1 of ourselves
            "Implementation-Title"    : 'SparkAPI',
            "Implementation-Version"  : api_version,
            "Implementation-Vendor"   : 'Lucko',
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
}

publishing {
    repositories {
        maven {
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username = findProperty('sonatypeUsername') ?: ''
                password = findProperty('sonatypePassword') ?: ''
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'spark'
                description = 'spark is a performance profiling plugin/mod for Minecraft clients, servers and proxies.'
                url = 'https://spark.lucko.me/'
            }
        }
    }
}