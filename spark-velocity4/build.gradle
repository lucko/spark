plugins {
    id 'net.kyori.blossom' version '1.3.0'
    id 'io.github.goooler.shadow' version '8.1.7'
}

tasks.withType(JavaCompile) {
    // override, compile targeting J11
    options.release = 11
}

dependencies {
    implementation project(':spark-common')
    compileOnly 'com.velocitypowered:velocity-api:4.0.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-annotation-processor:4.0.0-SNAPSHOT'
}

repositories {
    maven { url "https://repo.papermc.io/repository/maven-public/" }
}

blossom {
    replaceTokenIn('src/main/java/me/lucko/spark/velocity/VelocitySparkPlugin.java')
    replaceToken '@version@', project.pluginVersion
    replaceToken '@desc@', project.pluginDescription
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-velocity4.jar"

    dependencies {
        exclude(dependency('net.kyori:^(?!adventure-text-feature-pagination).+$'))
    }

    relocate 'net.kyori.adventure.text.feature.pagination', 'me.lucko.spark.lib.adventure.pagination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}