plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

tasks.withType(JavaCompile) {
    // override, compile targeting J17
    options.release = 17
}

dependencies {
    implementation project(':spark-common')

    compileOnly 'dev.hollowcube:minestom-ce:438338381e' // stable minestom version
    compileOnly 'dev.hollowcube:minestom-ce-extensions:1.2.0' // need this since ce removes extensions
    implementation 'com.google.guava:guava:19.0'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'extension.json'
    }
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-minestom.jar"

    dependencies {
        exclude(dependency('net.kyori:^(?!adventure-text-feature-pagination).+$'))
    }

    relocate 'net.kyori.adventure.text.feature.pagination', 'me.lucko.spark.lib.adventure.pagination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
